IMPORT prelude


§ `Promissory Note`

`Note Date` MEANS  "4 February 2024"

`Principal Amount` MEANS Money WITH 
                            Cents     IS 2500000
                            Currency  IS "USD"

`Interest Rate Basis Points Per Annum` MEANS 1500

`Security Collateral` MEANS NOTHING

`Monthly Installments` MEANS 12

`Default After Days Not Paid Beyond Due` MEANS 30

`Late Payment Penalty` MEANS Penalty WITH
                                `Interest Rate Basis Points` IS 500
                                `Grace Period Days`          IS 10

`Governing Law` MEANS "Republic of Singapore"


§§ `Parties`

`The Borrower` MEANS
  `Commercial Borrower` OF
    Company WITH
      `Name`           IS "Jane Dough Pte Ltd"
      `Address`        IS "42 Jane Doe Rd #04-20, Singapore 420000"
      `Jurisdiction`   IS "Singapore"
      `Company Number` IS "UEN 200424242N"
      `Account`        IS `Bank Account` WITH
                              `Bank Name`      IS "DBS Bank Ltd"
                              `Account Name`   IS "Jane Dough Pte Ltd"
                              `Account Number` IS "242-242424-9"
                              `SWIFT`          IS "DBSSSGSG"

`The Lender` MEANS
  `Individual Lender` OF
    `Natural Person` WITH
      `Name`          IS "John Doe"
      `Address`       IS "42 The Lender Rd, Singapore 424242"
      `Jurisdiction`  IS "Singapore"
      `ID Number`     IS "G5301960D"
      `Account`       IS `Bank Account` WITH
                              `Bank Name`      IS "DBS Bank Ltd"
                              `Account Name`   IS "The Lender"
                              `Account Number` IS "120-643240-1"
                              `SWIFT`          IS "DBSSSGSG"


§§ `Repayment of Principal & Interest Installments`

`Total Interest Amount` MEANS
    Money WITH
        Currency  IS `Principal Amount`'s Currency
        Cents     IS `Apply Interest Rate on`
                         (`Principal Amount`'s Cents)
                         `Interest Rate Basis Points Per Annum` DIVIDED BY
                              12 TIMES `Monthly Installments`

`Total Repayment Amount` MEANS
    Money WITH
        Currency  IS `Principal Amount`'s Currency
        Cents     IS `Principal Amount`'s Cents PLUS `Total Interest Amount`'s Cents

`Monthly Installment Amount` MEANS
    Money WITH
        Currency IS
            `Total Repayment Amount`'s Currency
        Cents IS
            `Total Repayment Amount`'s Cents DIVIDED BY
                `Monthly Installments`


GIVEN `Outstanding Payment Amount` IS A Money
`Payment Obligations` MEANS
    IF `Outstanding Payment Amount`'s Cents GREATER THAN 0
        THEN  PARTY   `The Borrower`
              MUST    `pay debts to`
                          `The Lender`
                          (`Next Payment Due`'s Amount)
              WITHIN  `Next Payment Due`'s `Days Beyond Commencement` PLUS
                          `Late Payment Penalty`'s `Grace Period Days`
              HENCE   `Payment Obligations`
                          (Money WITH
                              Currency  IS `Next Payment Due`'s Amount's Currency
                              Cents     IS `Outstanding Payment Amount`'s Cents MINUS
                                                `Next Payment Due`'s Amount's Cents)
--              LEST     PARTY   `The Borrower`
--                       MUST    `pay debts to`
--                                  `The Lender`
--                                  `Next Payment Due Amount With Penalty`
--                       WITHIN  `Penalty Grace Period`
--                       LEST    PARTY  `The Lender`
--                               MAY    `declare default of` `The Borrower`
--                               HENCE  PARTY   `The Borrower`
--                                      MUST    `pay debts to`
--                                                  `The Lender`
--                                                  `All Outstanding Debts`
        ELSE  FULFILLED

    WHERE
        `Installments Covered Since Commencement` MEANS
            (`Total Repayment Amount`'s Cents MINUS
                 `Outstanding Payment Amount`'s Cents) DIVIDED BY
                    `Monthly Installment Amount`'s Cents

        `Next Payment Due` MEANS
            Payment WITH
                Amount IS
                    Money WITH
                        Currency  IS `Monthly Installment Amount`'s Currency
                        Cents     IS `The lesser of`
                                         (`Monthly Installment Amount`'s Cents)
                                         (`Outstanding Payment Amount`'s Cents)
                `Days Beyond Commencement` IS
                    (`Installments Covered Since Commencement` PLUS 1) TIMES 30

        `Next Payment Due Amount With Penalty` MEANS
            Money WITH
                Currency  IS `Next Payment Due`'s Amount's Currency
                Cents     IS `Next Payment Due`'s Amount's Cents PLUS
                                  `Apply Interest Rate on`
                                      (`Next Payment Due`'s Amount's Cents)
                                      (`Late Payment Penalty`'s `Interest Rate Basis Points`)

        `Penalty Grace Period` MEANS
            `Next Payment Due`'s `Days Beyond Commencement` PLUS
                `Default After Days Not Paid Beyond Due`

        `All Outstanding Debts` MEANS
            Money WITH
                Currency  IS `Next Payment Due`'s Amount's Currency
                Cents     IS `Outstanding Payment Amount`'s Cents PLUS
                                `Next Payment Due Amount With Penalty`'s Cents MINUS
                                    `Next Payment Due`'s Amount's Cents



§§ `Testing`

#EVAL `Total Interest Amount`

#EVAL `Total Repayment Amount`

#EVAL `Monthly Installment Amount`

#EVAL EVALCONTRACT (`Payment Obligations` `Total Repayment Amount`)
                   (LIST  EVENT `The Borrower` (`pay debts to` `The Lender` (Money "USD" 239583)) 29
                          EVENT `The Borrower` (`pay debts to` `The Lender` (Money "USD" 239583)) 59
                          EVENT `The Borrower` (`pay debts to` `The Lender` (Money "USD" 239583)) 89
                          EVENT `The Borrower` (`pay debts to` `The Lender` (Money "USD" 239583)) 119
                          EVENT `The Borrower` (`pay debts to` `The Lender` (Money "USD" 239583)) 149
                          EVENT `The Borrower` (`pay debts to` `The Lender` (Money "USD" 239583)) 179
                          EVENT `The Borrower` (`pay debts to` `The Lender` (Money "USD" 239583)) 209
                          EVENT `The Borrower` (`pay debts to` `The Lender` (Money "USD" 239583)) 239
                          EVENT `The Borrower` (`pay debts to` `The Lender` (Money "USD" 239583)) 269
                          EVENT `The Borrower` (`pay debts to` `The Lender` (Money "USD" 239583)) 299
                          EVENT `The Borrower` (`pay debts to` `The Lender` (Money "USD" 239583)) 329
                          EVENT `The Borrower` (`pay debts to` `The Lender` (Money "USD" 239587)) 359)
                   360


#CONTRACT `Payment Obligations` `Total Repayment Amount` AT 360 WITH
    PARTY `The Borrower` DOES `pay debts to` `The Lender` (Money "USD" 239583) AT 29
    PARTY `The Borrower` DOES `pay debts to` `The Lender` (Money "USD" 239583) AT 59
    PARTY `The Borrower` DOES `pay debts to` `The Lender` (Money "USD" 239583) AT 89
    PARTY `The Borrower` DOES `pay debts to` `The Lender` (Money "USD" 239583) AT 119
    PARTY `The Borrower` DOES `pay debts to` `The Lender` (Money "USD" 239583) AT 149
    PARTY `The Borrower` DOES `pay debts to` `The Lender` (Money "USD" 239583) AT 179
    PARTY `The Borrower` DOES `pay debts to` `The Lender` (Money "USD" 239583) AT 209
    PARTY `The Borrower` DOES `pay debts to` `The Lender` (Money "USD" 239583) AT 239
    PARTY `The Borrower` DOES `pay debts to` `The Lender` (Money "USD" 239583) AT 269
    PARTY `The Borrower` DOES `pay debts to` `The Lender` (Money "USD" 239583) AT 299
    PARTY `The Borrower` DOES `pay debts to` `The Lender` (Money "USD" 239583) AT 329
    PARTY `The Borrower` DOES `pay debts to` `The Lender` (Money "USD" 239587) AT 359


§§ `Definitions`

DECLARE Money
    HAS Currency            IS A STRING
        Cents               IS A NUMBER

DECLARE Company
    HAS `Name`              IS A STRING
        `Address`           IS A STRING
        `Jurisdiction`      IS A STRING
        `Company Number`    IS A STRING
        `Account`           IS A `Bank Account`

DECLARE `Natural Person`
    HAS `Name`              IS A STRING
        `Address`           IS A STRING
        `Jurisdiction`      IS A STRING
        `ID Number`         IS A STRING
        `Account`           IS A `Bank Account`

DECLARE Borrower IS ONE OF
   `Individual Borrower`
        HAS Individual      IS A `Natural Person`
   `Commercial Borrower`
        HAS Entity          IS A Company

DECLARE Lender IS ONE OF
   `Individual Lender`
        HAS Individual      IS A `Natural Person`
   `Commercial Lender`
        HAS Entity          IS A Company

DECLARE `Bank Account`
    HAS `Bank Name`         IS A STRING
        `Account Name`      IS A STRING
        `Account Number`    IS A STRING
        `SWIFT`             IS A STRING

DECLARE Payment
    HAS Amount                       IS A Money
        `Days Beyond Commencement`   IS A NUMBER

DECLARE Penalty
    HAS `Interest Rate Basis Points` IS A NUMBER
        `Grace Period Days`          IS A NUMBER

DECLARE `pay debts to`
    HAS Recipient            IS A Lender
        Amount               IS A Money

DECLARE `declare default of`
    HAS Party                IS A Borrower

GIVEN Cents                  IS A NUMBER
      `Rate in Basis Points` IS A NUMBER
GIVETH A NUMBER 
`Apply Interest Rate on` MEANS
    Cents TIMES `Rate in Basis Points` DIVIDED BY 10000

GIVEN a IS A NUMBER
      b IS A NUMBER
`The lesser of` MEANS
    IF a GREATER THAN b
        THEN b
        ELSE a
